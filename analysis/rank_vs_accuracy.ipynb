{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Study of the rank of low-rank approximation\n",
    "\n",
    "#### Goal\n",
    "\n",
    "As per JCGS referee's request, we study the impact of the accuracy of low-rank approximation.\n",
    "\n",
    "We will vary rank (and relative tolerance, resp.) and see how accurate the approximated solution is,\n",
    "compared to the original solution.\n",
    "\n",
    "#### Analysis setup\n",
    "Before attempting to run this Julia code, make sure your computer is properly set up to run this code by following the setup instructions in the README of the git repository.\n",
    "\n",
    "We begin by loading the Distributions and LowRankApprox Julia packages, as well as some function definitions used in the code chunks below."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Distributions\n",
    "using LowRankApprox\n",
    "include(\"../code/datasim.jl\");\n",
    "include(\"../code/likelihood.jl\");\n",
    "include(\"../code/mixSQP.jl\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Next, initialize the sequence of pseudorandom numbers."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "srand(1);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### update a mixsqp function\n",
    "\n",
    "We allow mixsqp specifies the rank. Whether mixsqp uses \"rank\" or \"relative tolerance\" can be determined by \"lowrankcriterion\" argument."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "qpactiveset (generic function with 1 method)"
      ]
     },
     "execution_count": 14,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Input argument x specifies the initial iterate of the optimization\n",
    "# algorithm. When x = -1, or when any of the entries of x are\n",
    "# negative, the default setting for x is used.\n",
    "#\n",
    "# Since the MOSEK solver for the quadratic subproblem sometimes does\n",
    "# not tolerate iterates in which most of the entries are nonzero\n",
    "# (\"dense\" vectors), the default initial estimate for qpsubprob =\n",
    "# \"mosek\" is a sparse vector in which only the first two entries are\n",
    "# nonzero.\n",
    "function mixsqp(L; x = -1, rank = minimum(size(L)),\n",
    "                convtol = 1e-8, pqrtol = 1e-8, eps = 1e-6, sptol = 1e-3,\n",
    "                maxiter = 20, maxqpiter = 100,\n",
    "                lowrank = \"svd\", qpsubprob = \"activeset\",\n",
    "                nullprior = 0, lowrankcriterion = \"by_rank\",\n",
    "                linesearch = true,\n",
    "                verbose = true)\n",
    "    \n",
    "# Get the number of rows (n) and columns (k) of L.\n",
    "  n = size(L,1);\n",
    "  k = size(L,2);\n",
    "\n",
    "  # If any entries of input x are negative, set the initial estimate\n",
    "  # to the default setting.\n",
    "  #\n",
    "  # When the MOSEK solver is used, the initial estimate is set to a\n",
    "  # sparse vector in which all but two of the entries are zero.\n",
    "  if any(x .< 0)\n",
    "    if qpsubprob == \"activeset\"\n",
    "      x = ones(k)/k;\n",
    "    elseif qpsubprob == \"mosek\"\n",
    "      x = zeros(k);\n",
    "      x[1:2] = 1/2;\n",
    "    else\n",
    "      error(\"Input \\\"method\\\" should be \\\"activeset\\\" or \\\"mosek\\\"\");\n",
    "    end\n",
    "  end\n",
    "    \n",
    "  # If requested (i.e., if pqrtol > 0), compute the partial QR\n",
    "  # decomposition with relative precision \"tol\", then retrieve the\n",
    "  # permutation matrix, P. For details on the partial QR, see\n",
    "  # https://github.com/klho/LowRankApprox.jl.\n",
    "    \n",
    "  # Start timing for low-rank approximation of L.\n",
    "  if lowrank != \"none\" && qpsubprob == \"mosek\"\n",
    "    error(\"lowrank must be \\\"none\\\" when qpsubprob = \\\"mosek\\\"\");\n",
    "  end\n",
    "  tic();\n",
    "  if lowrankcriterion == \"by_rank\"\n",
    "    if lowrank == \"qr\"\n",
    "      F = pqrfact(L, rank = rank);\n",
    "      P = sparse(F[:P]);\n",
    "    elseif lowrank == \"svd\"\n",
    "      F = psvdfact(L, rank = rank);\n",
    "      S = Diagonal(F[:S]);\n",
    "    end\n",
    "  \n",
    "  else \n",
    "    if lowrank == \"qr\"\n",
    "      F = pqrfact(L, rtol=pqrtol);\n",
    "      P = sparse(F[:P]);\n",
    "    elseif lowrank == \"svd\"\n",
    "      F = psvdfact(L, rtol=pqrtol);\n",
    "      S = Diagonal(F[:S]);\n",
    "    end\n",
    "        \n",
    "  end\n",
    "  lowranktime = toq();\n",
    "    \n",
    "  # Summarize the analysis here.\n",
    "  if verbose\n",
    "    @printf(\"Running SQP algorithm with the following settings:\\n\")\n",
    "    @printf(\"- %d x %d data matrix\\n\",n,k)\n",
    "    @printf(\"- convergence tolerance = %0.2e\\n\",convtol)\n",
    "    @printf(\"- zero threshold        = %0.2e\\n\",sptol)\n",
    "    if lowrank == \"qr\"\n",
    "      err = maximum(abs.(F[:Q]*F[:R]*P' - L));\n",
    "      @printf(\"- partial QR tolerance  = %0.2e\\n\",pqrtol)\n",
    "      @printf(\"- partial QR max. error = %0.2e\\n\",err)\n",
    "    elseif lowrank == \"svd\"\n",
    "      err = maximum(abs.(F[:U]*S*F[:Vt] - L));\n",
    "      @printf(\"- partial SVD tolerance  = %0.2e\\n\",pqrtol)\n",
    "      @printf(\"- partial SVD max. error = %0.2e\\n\",err)\n",
    "    else\n",
    "      @printf(\"- Exact derivative computation (partial QR not used).\\n\")\n",
    "    end\n",
    "  end\n",
    "\n",
    "  # Initialize storage for the outputs obj, gmin, nnz and nqp.\n",
    "  obj      = zeros(maxiter);\n",
    "  gmin     = zeros(maxiter);\n",
    "  nnz      = zeros(Int,maxiter);\n",
    "  nqp      = zeros(Int,maxiter);\n",
    "  timing   = zeros(maxiter);\n",
    "  qptiming = zeros(maxiter);\n",
    "    \n",
    "  # Initialize loop variables used in the loops below so that they\n",
    "  # are available outside the scope of the loop.\n",
    "  i     = 0;\n",
    "  j     = 0;\n",
    "  D     = 0;\n",
    "  t     = 0;\n",
    "  numls = -1;\n",
    "    \n",
    "  # Print the column labels for reporting the algorithm's progress.\n",
    "  if verbose\n",
    "    @printf(\"iter      objective -min(g+1)  #nz #qp #ls\\n\")\n",
    "  end\n",
    "\n",
    "  # Repeat until we reach the maximum number of iterations, or until\n",
    "  # convergence is reached.\n",
    "  for i = 1:maxiter\n",
    "\n",
    "    # Start timing the iteration.\n",
    "    tic();\n",
    "      \n",
    "    # Compute the gradient and Hessian, optionally using the partial\n",
    "    # QR decomposition to increase the speed of these computations.\n",
    "    # gradient and Hessian computation -- Rank reduction method\n",
    "    if lowrank == \"qr\"\n",
    "      D = 1 ./(F[:Q]*(F[:R]*(P'*x)) .+ eps);\n",
    "      g = -P * F[:R]' * (F[:Q]'*D)/n;\n",
    "      H = P * F[:R]' * (F[:Q]'*Diagonal(D.^2)*F[:Q])*F[:R]*P'/n + eps * Diagonal(ones(k));\n",
    "    elseif lowrank == \"svd\"\n",
    "      D = 1 ./(F[:U]*(S*(F[:Vt]*x)) .+ eps);\n",
    "      g = -F[:Vt]'*(S * (F[:U]'*D))/n;\n",
    "      H = (F[:V]*S*(F[:U]'*Diagonal(D.^2)*F[:U])* S*F[:Vt])/n + eps * Diagonal(ones(k));\n",
    "    else\n",
    "      D = 1 ./(L*x .+ eps);\n",
    "      g = -L'*D/n;\n",
    "      H = L'*Diagonal(D.^2)*L/n + eps * Diagonal(ones(k));\n",
    "    end\n",
    "    if nullprior > 0\n",
    "      g[1] -= nullprior/x[1]/n;\n",
    "      H[1,1] += nullprior/x[1]^2/n;\n",
    "    end\n",
    "\n",
    "    # Report on the algorithm's progress.\n",
    "    if lowrank == \"qr\"\n",
    "      obj[i] = -sum(log.(F[:Q]*(F[:R]*(P'*x)) .+ eps));\n",
    "    elseif lowrank == \"svd\"\n",
    "      obj[i] = -sum(log.(F[:U]*(S*(F[:Vt]*x)) .+ eps));\n",
    "    else\n",
    "      obj[i] = mixobjective(L,x,eps);\n",
    "    end\n",
    "    gmin[i] = minimum(g) .+ 1;\n",
    "    nnz[i]  = length(find(x .> sptol));\n",
    "    nqp[i]  = j;\n",
    "    if verbose\n",
    "      if i == 1\n",
    "          @printf(\"%4d %0.8e %+0.2e %4d\\n\",\n",
    "              i,obj[i],-gmin[i],nnz[i]);\n",
    "      else\n",
    "          @printf(\"%4d %0.8e %+0.2e %4d %3d %3d\\n\",\n",
    "              i,obj[i],-gmin[i],nnz[i],nqp[i-1],numls);\n",
    "      end\n",
    "    end\n",
    "      \n",
    "    # Check convergence of outer loop\n",
    "    if gmin[i] >= -convtol\n",
    "      break\n",
    "    end\n",
    "    \n",
    "    # Solve the QP subproblem using either the active-set or\n",
    "    # interior-point (MOSEK) method.\n",
    "    out, qptiming[i], bytes, gctime,\n",
    "    memallocs = @timed if qpsubprob == \"activeset\"\n",
    "      y,nqp[i] = qpactiveset(x,g,H,convtol = convtol,sptol = sptol,\n",
    "                      maxiter = maxqpiter);\n",
    "    elseif qpsubprob == \"mosek\"\n",
    "      y = qpmosek(x,g,H);\n",
    "    end\n",
    "    \n",
    "    # Perform backtracking line search\n",
    "    if linesearch == true\n",
    "        for t = 1:10\n",
    "          if lowrank == \"qr\"\n",
    "            D_new = 1 ./(F[:Q]*(F[:R]*(P'*y)) .+ eps);\n",
    "          elseif lowrank == \"svd\"\n",
    "            D_new = 1 ./(F[:U]*(S*(F[:Vt]*y)) .+ eps);\n",
    "          else\n",
    "            D_new = 1 ./(L*y .+ eps);\n",
    "          end\n",
    "          if all(D_new .> 0)\n",
    "            if sum(log.(D)) - sum(log.(D_new)) > sum((x-y) .* g) / 2\n",
    "              break\n",
    "            end\n",
    "          end\n",
    "          y = (y-x)/2 + x;\n",
    "        end\n",
    "        numls = t;\n",
    "    else\n",
    "        numls = -1;\n",
    "    end\n",
    "\n",
    "    # Update the solution to the original optimization problem.\n",
    "    x = copy(y);\n",
    "\n",
    "    # Get the elapsed time for the ith iteration.\n",
    "    timing[i] = toq();\n",
    "  end\n",
    "\n",
    "  # Return: (1) the solution (after zeroing out any values below the\n",
    "  # tolerance); (2) the value of the objective at each iteration; (3)\n",
    "  # the minimum gradient value of the modified objective at each\n",
    "  # iteration; (4) the number of nonzero entries in the vector at each\n",
    "  # iteration; and (5) the number of inner iterations taken to solve\n",
    "  # the QP subproblem at each outer iteration.\n",
    "  x[x .< sptol] .= 0;\n",
    "  x             = x/sum(x);\n",
    "  totaltime     = lowranktime + sum(timing[1:i]);\n",
    "  if verbose\n",
    "    @printf(\"Optimization took %d iterations and %0.4f seconds.\\n\",i,totaltime)\n",
    "  end\n",
    "  return Dict([(\"x\",Vector(x)), (\"totaltime\",totaltime),\n",
    "               (\"lowranktime\",lowranktime), (\"obj\",obj[1:i]),\n",
    "               (\"gmin\",gmin[1:i]), (\"nnz\",nnz[1:i]),\n",
    "               (\"nqp\",nqp[1:i]), (\"timing\",timing[1:i]),\n",
    "               (\"qptiming\",qptiming[1:i])])\n",
    "end\n",
    "\n",
    "# Solve the QP subproblem for the mix-SQP algorithm using an active\n",
    "# set method.\n",
    "function qpactiveset(x, g, H; convtol = 1e-8, sptol = 1e-3, maxiter = 100)\n",
    "\n",
    "  # Get the number of degrees of freedom in the optimization problem.\n",
    "  k = length(x);\n",
    "    \n",
    "  # Initialize the solution to the QP subproblem.\n",
    "  y      = sparse(zeros(k));\n",
    "  ind    = find(x .> sptol);\n",
    "  y[ind] .= 1/length(ind);\n",
    "  i = 0;\n",
    "\n",
    "  # Repeat until we reach the maximum number of iterations, or until\n",
    "  # convergence is reached.\n",
    "  for i = 1:maxiter\n",
    "        \n",
    "    # Define the smaller QP subproblem.\n",
    "    s   = length(ind);\n",
    "    H_s = H[ind,ind];\n",
    "    d   = H*y + 2*g .+ 1;\n",
    "    d_s = d[ind];\n",
    "\n",
    "    # Solve the smaller problem.\n",
    "    p      = sparse(zeros(k));\n",
    "    p_s    = -H_s\\d_s;\n",
    "    p[ind] = p_s;\n",
    "\n",
    "    # Check convergence using KKT\n",
    "    if norm(p_s) < convtol\n",
    "            \n",
    "      # Compute the Lagrange multiplier.\n",
    "      z = d;\n",
    "      if all(z .>= -convtol)\n",
    "        break\n",
    "      elseif length(ind) < k\n",
    "        notind  = setdiff(1:k,ind);\n",
    "        ind_min = notind[findmin(z[notind])[2]];\n",
    "        ind     = sort([ind; ind_min]);\n",
    "      end\n",
    "    else\n",
    "          \n",
    "      # find a feasible step length.\n",
    "      alpha     = 1;\n",
    "      alpha0    = -y[ind]./p_s;\n",
    "      ind_block = find(p_s .< 0);\n",
    "      alpha0    = alpha0[ind_block];\n",
    "      if ~isempty(ind_block)\n",
    "        v, t = findmin(alpha0);\n",
    "        if v < 1\n",
    "\n",
    "          # Blocking constraint.\n",
    "          ind_block = ind[ind_block[t]]; \n",
    "          alpha     = v;\n",
    "              \n",
    "          # Update working set if there is a blocking constraint.\n",
    "          deleteat!(ind,find(ind .- ind_block .== 0));\n",
    "        end\n",
    "      end\n",
    "          \n",
    "      # Move to the new \"inner loop\" iterate (y) along the search\n",
    "      # direction.\n",
    "      y = y + alpha * p;\n",
    "    end\n",
    "  end\n",
    "\n",
    "  # Return the solution to the quadratic program.\n",
    "  return y, Int(i)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Write a function for simulation\n",
    "\n",
    "Let's start with writing a function for the simulation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 36,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "sim_rank (generic function with 2 methods)"
      ]
     },
     "execution_count": 36,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function sim_rank(n, m, repnum = 5)\n",
    "    \n",
    "    \n",
    "    # SIMULATE the data matrix L n times m\n",
    "    z = normtmixdatasim(round(Int,n));\n",
    "    sd = autoselectmixsd(z, nv = m);\n",
    "    L  = normlikmatrix(z, sd = sd);\n",
    "    \n",
    "    # RUN mixsqp without low-rank approximation\n",
    "    x_true = mixsqp(L, lowrank = \"none\", verbose = false)[\"x\"];\n",
    "    \n",
    "    # PREALLOCATE memory for the output\n",
    "    df     = zeros(m, repnum);\n",
    "    dxl0   = zeros(m, repnum);\n",
    "    dxl1   = zeros(m, repnum);\n",
    "    t      = zeros(m, repnum);\n",
    "    \n",
    "    # RUN mixsqp\n",
    "    for rank = 1:m\n",
    "        for iter = 1:repnum\n",
    "            tic();\n",
    "            out               = mixsqp(L, rank = rank, verbose = false);\n",
    "            x                 = out[\"x\"];\n",
    "            df[rank,iter]     = mixobjective(L,x_true) - mixobjective(L,x);\n",
    "            dxl0[rank,iter]   = maximum(abs.(x_true - x));\n",
    "            dxl1[rank,iter]   = norm(x_true - x, 1);\n",
    "            t[rank,iter]      = toq();\n",
    "        end\n",
    "        \n",
    "        println(rank);\n",
    "    end\n",
    "    \n",
    "    return Dict([\n",
    "                (:df, df), (:dx0, dxl0), (:dx1, dxl1), (:t, t)\n",
    "                ])\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1\n",
      "2\n",
      "3\n",
      "4\n",
      "5\n",
      "6\n",
      "7\n",
      "8\n",
      "9\n",
      "10\n",
      "11\n",
      "12\n",
      "13\n",
      "14\n",
      "15\n",
      "16\n",
      "17\n",
      "18\n",
      "19\n"
     ]
    }
   ],
   "source": [
    "out = sim_rank(100000,50)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
